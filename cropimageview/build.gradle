plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.valueOf("$envVersionCode")
        versionName "$envVersionName"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.exifinterface:exifinterface:1.3.3'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation('androidx.test.ext:junit:1.1.3') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0') {
        exclude module: 'support-annotations'
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/carousell/cropimageview")
                credentials {
                    username = project.findProperty("github.packages.username")
                    password = project.findProperty("github.packages.token")
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = "com.github.carousell"
                artifactId = "cropimageview"
                version = "$envVersionName"

                from components.release
            }
        }
    }
}